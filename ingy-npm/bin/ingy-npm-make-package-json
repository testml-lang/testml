#!/usr/bin/env coffee

yaml = require 'js-yaml'
fs = require 'fs'

meta = yaml.load String fs.readFileSync 'Meta'

npm = meta['=npm'] || {}

plan =
  name: 'name'
  description: 'abstract'
  version: 'version'
  keywords: 'tag'
  homepage: 'homepage'
  bugs:
    url: 'devel/bug'
  repository:
    type: '"git"'
    url: 'devel/git'
  license: 'license'
  author:
    name: 'author/name'
    email: 'author/email'
    url: 'author/homepage'
  dependencies: 'dependencies'
  devDependencies: 'devDependencies'
  main: 'main'
  bin: 'bin'
  files: 'files'
  directories: 'directories'
  scripts: 'scripts'
  engines: 'engines'

make_package_json = ->
  pkg = {}

  for key, val of plan
    if npm[key]
      pkg[key] = npm[key]
    else if typeof val == 'object'
      if value = get_object val
        pkg[key] = value
    else
      if value = get_key val
        pkg[key] = value

  return pkg

get_key = (key)->
  if key.match /^"/
    return key.replace /"/g, ''
  if key.match /\//
    [key1, key2] = key.split '/'
    val = meta[key1]
    return unless val
    return val[key2]
  return meta[key]

get_object = (plan)->
  obj = {}
  for key, val of plan
    if value = get_key val
      obj[key] = value
  return unless Object.keys(obj).length
  return obj


console.log JSON.stringify make_package_json(), null, 2
